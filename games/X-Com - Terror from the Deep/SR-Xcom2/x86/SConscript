#
#  Copyright (C) 2016-2023 Roman Pauer
#
#  Permission is hereby granted, free of charge, to any person obtaining a copy of
#  this software and associated documentation files (the "Software"), to deal in
#  the Software without restriction, including without limitation the rights to
#  use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies
#  of the Software, and to permit persons to whom the Software is furnished to do
#  so, subject to the following conditions:
#
#  The above copyright notice and this permission notice shall be included in all
#  copies or substantial portions of the Software.
#
#  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
#  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
#  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
#  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
#  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
#  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
#  SOFTWARE.
#

import re

Import('device', 'executable', 'version', 'env')

include_re = re.compile(r'^%include\s+"(\S+)"\r?$', re.M)

def nasmfile_scan(node, env, path):
    contents = node.get_text_contents()
    includes = include_re.findall(contents)
    if len(includes) > 0:
        dirparts = node.dir.path.split('/')
        if len(dirparts) >= 2 and dirparts[0] == 'x86':
            parentdir = node.Dir('..')
            includes = ['../' + include if not node.File(include).exists() and parentdir.File(include).exists() else include for include in includes]
    return includes

nasmscan = Scanner(function = nasmfile_scan,
                   skeys = ['.asm'],
                   recursive = True)

SourceFileScanner.add_scanner('.asm', nasmscan)

if device == 'pc-linux':
    env2 = Environment(tools=['nasm'], ASFLAGS = ' -felf32 -Ox -w+orphan-labels -ix86/')
else:
    env2 = Environment(tools=['nasm'], ASFLAGS = ' -fwin32 -Ox -w+orphan-labels -ix86/')

obj = env.Object(Glob('*.c')) + env2.Object(Glob('*.asm'))

if executable == 'combined' or executable == 'geoscape':
    obj = obj + SConscript('geoscape/SConscript', exports='device version')

if executable == 'combined' or executable == 'tactical':
    obj = obj + SConscript('tactical/SConscript', exports='device version')

Return('obj')
