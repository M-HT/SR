#
#  Copyright (C) 2016 Roman Pauer
#
#  Permission is hereby granted, free of charge, to any person obtaining a copy of
#  this software and associated documentation files (the "Software"), to deal in
#  the Software without restriction, including without limitation the rights to
#  use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies
#  of the Software, and to permit persons to whom the Software is furnished to do
#  so, subject to the following conditions:
#
#  The above copyright notice and this permission notice shall be included in all
#  copies or substantial portions of the Software.
#
#  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
#  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
#  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
#  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
#  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
#  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
#  SOFTWARE.
#

import re

Import('device', 'version')

include_re = re.compile(r'^%include\s+"(\S+)"\r?$', re.M)

def nasmfile_scan(node, env, path):
    contents = node.get_text_contents()
    includes = include_re.findall(contents)
    return includes

nasmscan = Scanner(function = nasmfile_scan,
                   skeys = ['.asm'],
                   recursive = True)

SourceFileScanner.add_scanner('.asm', nasmscan)

if device == 'pc-linux':
    env = Environment(tools=['nasm'], ASFLAGS = ' -felf -dELF -Ox -w+orphan-labels -w-number-overflow -ix86/')
    env2 = Environment(tools=['nasm'], ASFLAGS = ' -felf -dELF -O1 -w+orphan-labels -w-number-overflow -ix86/')

    if version == 'cd':
        obj = env2.Object('war-asm-cd_linux.asm')
    else:
        obj = env2.Object('war-asm_linux.asm')
else:
    env = Environment(tools=['nasm'], ASFLAGS = ' -fwin32 -Ox -w+orphan-labels -w-number-overflow -ix86/')
    env2 = Environment(tools=['nasm'], ASFLAGS = ' -fwin32 -O1 -w+orphan-labels -w-number-overflow -ix86/')

    if version == 'cd':
        obj = env2.Object('war-asm-cd.asm')
    else:
        obj = env2.Object('war-asm.asm')

asm_sources = Glob('*.asm', strings = True)
asm_sources.remove('war-asm.asm')
asm_sources.remove('war-asm_linux.asm')
asm_sources.remove('war-asm-cd.asm')
asm_sources.remove('war-asm-cd_linux.asm')

obj = obj + env.Object(asm_sources)

Return('obj')
